void LeadOwnerUpdate(string payload)
{
// -----------------------------------------------------------------------------------------------
// This function executes when Zoho SalesIQ sends webhook when ..
// .. a chat has been created. This will then pull the operator and ..
// .. update the generated lead record to sync the operator as Lead Owner.
// -----------------------------------------------------------------------------------------------
if(payload.size() != 0)
{
	info "With payload.";
	// Get Payload Data
	entityData = payload.get("entity");
	convfId = entityData.get("id");
	visitorName = entityData.get("visitor").get("name");
	leadSource = "Facebook";
	fetchConvf = invokeurl
	[
		url :"https://salesiq.zoho.com/api/v2/dmcihomes5226/conversations/" + convfId
		type :GET
		connection:"salesiq_connections"
	];
	info "sales iq fetch conversations";
	info fetchConvf;
	routedUsers = fetchConvf.get("data").get("routed_users");
	if(fetchConvf.get("data").containKey("routed_users"))
	{
		assignedOperatorId = fetchConvf.get("data").get("routed_users").get(0);
	}
	else if(fetchConvf.get("data").containKey("attender"))
	{
		assignedOperatorId = fetchConvf.get("data").get("attender").get("id");
	}
	if(fetchConvf.size() != 0)
	{
		routedUserId = assignedOperatorId;
		pageName = fetchConvf.get("data").get("visitor").get("channel_details").get("source");
		// -----------------------------------------------------------------------------------------------
		// Get Operator
		// -----------------------------------------------------------------------------------------------
		fetchOperator = invokeurl
		[
			url :"https://salesiq.zoho.com/api/v2/dmcihomes5226/operators/" + routedUserId
			type :GET
			connection:"salesiq_connections"
		];
		info "sales iq fetch operators";
		info fetchOperator;
		operatorEmail = fetchOperator.get("data").get("email_id");
		// -----------------------------------------------------------------------------------------------
		// Pull User Data in CRM
		// -----------------------------------------------------------------------------------------------
		userData = zoho.crm.searchRecords("users","(email:equals:" + operatorEmail + ")");
		info "CRM search record";
		info userData;
		userCrmId = userData.get("users").get(0).get("id");
		// -----------------------------------------------------------------------------------------------
		// Pull Lead Record in CRM
		// -----------------------------------------------------------------------------------------------
		querySelect = "select id, Full_Name, Lead_Status, Created_Time from Leads ";
		// queryWhere = "where Full_Name = '" + visitorName + "' and Lead_Status ='New Prospect'";
		queryWhere = "where (((Full_Name = '" + visitorName + "') and Lead_Source ='" + leadSource + "') and Lead_Status ='New Prospect')";
		queryLimit = " limit 200";
		query = querySelect + queryWhere + queryLimit;
		queryMap = Map();
		queryMap.put("select_query",query);
		searchLeadsMetaData = invokeurl
		[
			url :"https://www.zohoapis.com/crm/v7/coql"
			type :POST
			parameters:queryMap.toString()
			connection:"flow_to_crm_connections"
		];
		info "CRM coql";
		info searchLeadsMetaData;
		if(searchLeadsMetaData != null && searchLeadsMetaData.get("data") != null)
		{
			leadId = searchLeadsMetaData.get("data").get(0).get("id");
			// -----------------------------------------------------------------------------------------------
			// Update Lead Owner
			// -----------------------------------------------------------------------------------------------
			param = Map();
			param.put("Owner",userCrmId);
			param.put("Leads_Origin",pageName);
			updateLeadRecord = zoho.crm.updateRecord("Leads",leadId,param,{"trigger":{"workflow"}});
			info "CRM update record";
			info updateLeadRecord;
		}
		else
		{
			info "No lead captured.";
		}
	}
	else
	{
		info "No conversation found.";
	}
}
else
{
	info "Payload empty.";
}
// END
// -----------------------------------------------------------------------------------------------
}